prototype(Medienreaktor.Bootstrap:Component.Navbar) < prototype(Medienreaktor.Bootstrap:Component) {
    tagName = 'nav'
    className = 'navbar'
    theme = 'light'
    expand = 'lg'
    fluid = false
    fixedTop = false
    fixedBottom = false
    stickyTop = false
    toggler = true

    # array {title: 'Title', href: 'http://', img: 'http://', alt: 'Alternative text'}
    brand = null

    # array [{title: 'Title', href: 'http://', active: false}, {...}]
    items = null

    renderer = afx`
        <Medienreaktor.Bootstrap:Augmenter id={props.id} className={props.className} classNames={{'fixed-top': props.fixedTop, 'fixed-bottom': props.fixedBottom, 'sticky-top': props.stickyTop}} classNamesAppend={{'expand': props.expand}} theme={props.theme} background={props.theme}>
            <Neos.Fusion:Tag tagName={props.tagName}>
                <Medienreaktor.Bootstrap:Layout.Container fluid={props.fluid}>
                    <Medienreaktor.Bootstrap:Component.NavbarBrand brand={props.brand} @if.hasBrand={props.brand} />
                    <Medienreaktor.Bootstrap:Component.NavbarToggler target="navbar-collapse" @if.showToggler={props.toggler} />
                    <div class="collapse navbar-collapse" id="navbar-collapse">
                        <Medienreaktor.Bootstrap:Component.Nav items={props.items} className="navbar-nav" />
                    </div>
                </Medienreaktor.Bootstrap:Layout.Container>
            </Neos.Fusion:Tag>
        </Medienreaktor.Bootstrap:Augmenter>
    `
}

prototype(Medienreaktor.Bootstrap:Component.NavbarToggler) < prototype(Medienreaktor.Bootstrap:Component) {
    tagName = 'button'
    className = 'navbar-toggler'
    target = ''

    renderer = afx`
        <Medienreaktor.Bootstrap:Augmenter id={props.id} className={props.className}>
            <Neos.Fusion:Tag tagName={props.tagName} attributes.type="button" attributes.data-toggle="collapse" attributes.data-target={'#' + props.target}>
                <Medienreaktor.Bootstrap:Component.NavbarTogglerIcon />
            </Neos.Fusion:Tag>
        </Medienreaktor.Bootstrap:Augmenter>
    `
}

prototype(Medienreaktor.Bootstrap:Component.NavbarTogglerIcon) < prototype(Medienreaktor.Bootstrap:Component) {
    tagName = 'span'
    className = 'navbar-toggler-icon'

    renderer = afx`
        <Medienreaktor.Bootstrap:Augmenter id={props.id} className={props.className}>
            <Neos.Fusion:Tag tagName={props.tagName}></Neos.Fusion:Tag>
        </Medienreaktor.Bootstrap:Augmenter>
    `
}

prototype(Medienreaktor.Bootstrap:Component.NavbarBrand) < prototype(Medienreaktor.Bootstrap:Component) {
    tagName = 'div'
    className = 'navbar-brand'

    # array {title: 'Title', href: 'http://', img: 'http://', alt: 'Alternative text'}
    brand = null

    title = ${this.brand.title}
    href = ${this.brand.href}
    img = ${this.brand.img}
    alt = ${this.brand.alt}

    renderer = afx`
        <Medienreaktor.Bootstrap:Augmenter id={props.id} className={props.className} href={props.href}>
            <Neos.Fusion:Tag tagName={props.href ? 'a' : props.tagName}>
                <Medienreaktor.Bootstrap:Content.Image src={props.img} alt={props.alt} @if.hasImage={props.img} />
                {props.title}
            </Neos.Fusion:Tag>
        </Medienreaktor.Bootstrap:Augmenter>
    `
}
